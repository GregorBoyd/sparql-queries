PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX qb: <http://purl.org/linked-data/cube#>

SELECT  ?Local_Authority ?area ?population (ROUND(xsd:integer((?population)/(?area)*100)) AS ?density_km2)
WHERE  {
  ?s <http://purl.org/linked-data/sdmx/2009/dimension#refArea> ?LA.
  ?LA <http://statistics.data.gov.uk/def/statistical-entity#code> <http://statistics.gov.scot/id/statistical-entity/S12>.
  ?LA rdfs:label ?Local_Authority.
  ?s <http://statistics.gov.scot/def/measure-properties/count> ?area.
  ?s qb:dataSet <http://statistics.gov.scot/data/geographic-area-2001>.
  ?d <http://purl.org/linked-data/sdmx/2009/dimension#refArea> ?LA.
  ?d <http://statistics.gov.scot/def/measure-properties/count> ?population.
  ?d qb:dataSet <http://statistics.gov.scot/data/population-estimates>.
  ?d <http://statistics.gov.scot/def/dimension/gender> <http://statistics.gov.scot/def/concept/gender/all>.
  ?d <http://statistics.gov.scot/def/dimension/age> <http://statistics.gov.scot/def/concept/age/all>.
  ?d <http://purl.org/linked-data/sdmx/2009/dimension#refPeriod> <http://reference.data.gov.uk/id/year/2014>.
  }

ORDER BY DESC(?density_km2)
LIMIT 100
